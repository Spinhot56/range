// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.13;

import './PrecisionMath.sol';

/// @notice Math library that facilitates ranged liquidity calculations.
library DyDxMath {
    uint256 internal constant Q96 = 0x1000000000000000000000000;

    error PriceOutsideBounds();

    function getDy(
        uint256 liquidity,
        uint256 priceLower,
        uint256 priceUpper,
        bool roundUp
    ) external pure returns (uint256 dy) {
        return _getDy(liquidity, priceLower, priceUpper, roundUp);
    }

    function getDx(
        uint256 liquidity,
        uint256 priceLower,
        uint256 priceUpper,
        bool roundUp
    ) external pure returns (uint256 dx) {
        return _getDx(liquidity, priceLower, priceUpper, roundUp);
    }

    function _getDy(
        uint256 liquidity,
        uint256 priceLower,
        uint256 priceUpper,
        bool roundUp
    ) internal pure returns (uint256 dy) {
        unchecked {
            if (roundUp) {
                dy = PrecisionMath.mulDivRoundingUp(liquidity, priceUpper - priceLower, Q96);
            } else {
                dy = PrecisionMath.mulDiv(liquidity, priceUpper - priceLower, Q96);
            }
        }
    }

    function _getDx(
        uint256 liquidity,
        uint256 priceLower,
        uint256 priceUpper,
        bool roundUp
    ) internal pure returns (uint256 dx) {
        if (roundUp) {
            dx = PrecisionMath.divRoundingUp(
                PrecisionMath.mulDivRoundingUp(
                    liquidity << 96,
                    priceUpper - priceLower,
                    priceUpper
                ),
                priceLower
            );
        } else {
            dx =
                PrecisionMath.mulDiv(liquidity << 96, priceUpper - priceLower, priceUpper) /
                priceLower;
        }
    }

    //TODO: debug math for this to validate numbers
    function getLiquidityForAmounts(
        uint256 priceLower,
        uint256 priceUpper,
        uint256 currentPrice,
        uint256 dy,
        uint256 dx
    ) external pure returns (uint256 liquidity) {
        unchecked {
            if (priceUpper <= currentPrice) {
                liquidity = PrecisionMath.mulDiv(
                    dy,
                    0x1000000000000000000000000,
                    priceUpper - priceLower
                );
            } else if (currentPrice <= priceLower) {
                liquidity = PrecisionMath.mulDiv(
                    dx,
                    PrecisionMath.mulDiv(priceLower, priceUpper, 0x1000000000000000000000000),
                    priceUpper - priceLower
                );
            } else {
                uint256 liquidity0 = PrecisionMath.mulDiv(
                    dx,
                    PrecisionMath.mulDiv(priceUpper, currentPrice, 0x1000000000000000000000000),
                    priceUpper - currentPrice
                );
                uint256 liquidity1 = PrecisionMath.mulDiv(
                    dy,
                    0x1000000000000000000000000,
                    currentPrice - priceLower
                );
                liquidity = liquidity0 < liquidity1 ? liquidity0 : liquidity1;
            }
        }
    }

    function getAmountsForLiquidity(
        uint256 priceLower,
        uint256 priceUpper,
        uint256 currentPrice,
        uint256 liquidityAmount,
        bool roundUp
    ) internal pure returns (uint128 token0amount, uint128 token1amount) {
        if (currentPrice <= priceLower) {
            // token0 (X) is supplied
            token0amount = uint128(_getDx(liquidityAmount, priceLower, priceUpper, roundUp));
        } else if (priceUpper <= currentPrice) {
            // token1 (y) is supplied
            token1amount = uint128(_getDy(liquidityAmount, priceLower, priceUpper, roundUp));
        } else {
            // Both token0 (x) and token1 (y) are supplied
            token0amount = uint128(_getDx(liquidityAmount, currentPrice, priceUpper, roundUp));
            token1amount = uint128(_getDy(liquidityAmount, priceLower, currentPrice, roundUp));
        }
    }
}
